root ::= object

# Strict adjudication output schema:
# {"final_lines":[...], "used":[...], "confidence":"high|medium|low", "notes":[...]}
object ::=
  "{" ws
    "\"final_lines\"" ws ":" ws string_array ws "," ws
    "\"used\"" ws ":" ws used_array ws "," ws
    "\"confidence\"" ws ":" ws confidence ws "," ws
    "\"notes\"" ws ":" ws string_array
  ws "}" ws

used_array ::= "[" ws used_item (ws "," ws used_item)* ws "]" ws
used_item ::= "\"baseline\"" | "\"recover\"" | "\"alt_asr\""

confidence ::= "\"high\"" | "\"medium\"" | "\"low\""

string_array ::= "[" ws (string (ws "," ws string)*)? ws "]" ws

# JSON string with standard escapes; disallow control chars
string ::=
  "\"" (
    [^"\\\x7F\x00-\x1F] |
    "\\" (["\\/bfnrt] | "u" [0-9a-fA-F]{4})
  )* "\"" ws

ws ::= ([ \t\n] ws)?